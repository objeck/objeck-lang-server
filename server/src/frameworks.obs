use Collection.Generic;
use System.IO.Net;
use Data.JSON;

bundle LSP {
	#~
	Abstract socket server worker
	~#
	class LspWorker from System.Concurrency.Thread {
		@address : String;
		@port : Int;
		@quit : Bool;
		@is_initialized : Bool;
		@is_debug : Bool;
		@workspace : LspWorkspace;
		@socket : TCPSocket;
		@id : Int;

		function : Init() ~ Nil {
		}

		New(socket : TCPSocket, is_debug : Bool) {
			Parent();
			@socket := socket;
			@is_debug := is_debug;
			@workspace := LspWorkspace->New();
		}

		method : public : Run(param : Base) ~ Nil {
			while(<>@quit) {
				request := ReadRpcRequest();
				if(request <> Nil) {
					ProcessResponse(request);
				}	
				else {
					@quit := true;
				};
			};

			@socket->Close();
		}

		method : public : IsDebug() ~ Bool {
			return @is_debug;
		}

		#
		# Notifications
		#
		method : virtual : NotifyInitialized(params : JsonElement) ~ Nil;
		method : virtual : NotifyCancel(params : JsonElement) ~ Nil;
		method : virtual : NotifyDidOpen(params : JsonElement) ~ Nil;
		method : virtual : NotifyDidChange(params : JsonElement) ~ Nil;
		method : virtual : NotifyDidChangeWatchedFiles(params : JsonElement) ~ Nil;
		method : virtual : NotifyDidSave(params : JsonElement) ~ Nil;
		method : virtual : NotifyDidClose(params : JsonElement) ~ Nil;
		method : virtual : NotifyDidChangeWorkspace(params : JsonElement) ~ Nil;

		#
		# Callbacks
		#
		method : virtual : CallbackInitialize(id : Int, params : JsonElement) ~ Nil;
		method : virtual : CallbackShutdown(id : Int, params : JsonElement) ~ Nil;
		method : virtual : CallbackCompletion(id : Int, params : JsonElement) ~ Nil;
		method : virtual : CallbackResolve(id : Int, params : JsonElement) ~ Nil;
		method : virtual : CallbackDocumentSymbol(id : Int, params : JsonElement) ~ Nil;
		method : virtual : CallbackReferences(id : Int, params : JsonElement) ~ Nil;
		method : virtual : CallbackDeclaration(id : Int, params : JsonElement) ~ Nil;
		method : virtual : SignatureHelp(id : Int, params : JsonElement) ~ Nil;

		# --- process requests ---
		method : ProcessResponse(request : RpcRequest) ~ Nil {
			id := request->GetId();
			method_id := request->GetMethod();
			
			# notification
			if(id->IsNeg()) {
				if(@is_debug) {
					"NOTIFICATION='{$method_id}'\n"->PrintLine();
				};

				if(method_id->Equals("initialized")) {
					NotifyInitialized(request->GetParams());
				}
				else if(method_id->Equals("$/cancelRequest")) {
					NotifyCancel(request->GetParams());
				}
				else if(method_id->Equals("workspace/didChangeWatchedFiles")) {
					NotifyDidChangeWatchedFiles(request->GetParams());
				}
				else if(method_id->Equals("textDocument/didOpen")) {
					NotifyDidOpen(request->GetParams());
				}
				else if(method_id->Equals("textDocument/didChange")) {
					NotifyDidChange(request->GetParams());
				}
				else if(method_id->Equals("textDocument/didSave")) {
					NotifyDidSave(request->GetParams());
				}
				else if(method_id->Equals("textDocument/didClose")) {
					NotifyDidClose(request->GetParams());
				}
				else if(method_id->Equals("workspace/didChangeWorkspaceFolders")) {
					NotifyDidChangeWorkspace(request->GetParams());
				}
				else {
					if(@is_debug) {
						"*** Unknown notification='{$method_id}' ***"->PrintLine();
					};
				};
			}
			# callback
			else {
				if(@is_debug) {
					"METHOD='{$method_id}', id={$id}"->PrintLine();
				};

				if(method_id->Equals("initialize")) {
					CallbackInitialize(id, request->GetParams());
				}
				else if(method_id->Equals("textDocument/completion")) {
					CallbackCompletion(id, request->GetParams());
				}
				else if(method_id->Equals("completionItem/resolve")) {
					CallbackResolve(id, request->GetParams());
				}
				else if(method_id->Equals("textDocument/documentSymbol")) {
					CallbackDocumentSymbol(id, request->GetParams());
				}
				else if(method_id->Equals("textDocument/references")) {
					CallbackReferences(id, request->GetParams());
				}
				else if(method_id->Equals("textDocument/declaration")) {
					CallbackDeclaration(id, request->GetParams());
				}
				else if(method_id->Equals("textDocument/signatureHelp")) {
					SignatureHelp(id, request->GetParams());
				}
				else if(method_id->Equals("shutdown")) {
					CallbackShutdown(id, request->GetParams());
				}
				else {
					if(@is_debug & <>method_id->Equals("<none>")) {
						"*** Unknown method='{$method_id}' ***"->PrintLine();
					};
				};
			};
		}

		# --- utilities ---
		method : ReadString() ~ String {
			line := @socket->ReadLine();
			@socket->ReadLine();

			index := line->Find(':');
			if(index > 0) {
				index += 2;
				length := line->SubString(index, line->Size() - index)->ToInt();
				# TODO: check length (buffer overflow)
				buffer := Byte->New[length];
				@socket->ReadBuffer(0, buffer->Size(), buffer);

				text := String->New(buffer->ToUnicode());
				if(@is_debug) {
					"====\nREAD='{$text}'"->PrintLine();
				};

				return text;
			};

			return "";
		}

		method : WriteString(text : String) ~ Nil {
			if(@is_debug) {
				"WRITE='{$text}'"->PrintLine();
			};		
			length := text->Size();
			@socket->WriteString("Content-Length: {$length}\r\n\r\n{$text}");
		}

		method : ReadRpcRequest() ~ RpcRequest {
			text := ReadString();
			if(<>text->IsEmpty()) {
				json :=JsonParser->New(text);
				if(json->Parse()) {
					node := json->GetRoot();
					
					id := -1;
					id_elem := node->Get("id");
					if(id_elem <> Nil) {
						id := id_elem->GetValue()->ToInt();
					};

					ver := node->Get("jsonrpc");
					
					mthd : String;
					mthd_elem := node->Get("method");
					if(mthd_elem <> Nil) {
						mthd := mthd_elem->GetValue();
					}
					else {
						mthd := "<none>";
					};

					params := node->Get("params");
					if(params = Nil) {
						params := node->Get("result");
					};

					if(ver <> Nil & ver->GetValue()->Equals("2.0") & mthd <> Nil) {
						return RpcRequest->New(id, mthd, params);
					};
				};
			};

			return Nil;
		}

		# --- capability ---
		method : WriteRpcCapability(id : Int, params : JsonElement) ~ Nil {
			node := JsonElement->New(JsonElement->JsonType->OBJECT);
			
			node->Insert("jsonrpc", "2.0");
			node->Insert("method", "client/registerCapability");
			node->Insert("params", params);
			node->Insert("id", id);

			WriteString(node->ToString());
		}

		# --- response ---
		method : WriteRpcResponse(id : Int, result : JsonElement) ~ Nil {
			node := JsonElement->New(JsonElement->JsonType->OBJECT);

			node->Insert("jsonrpc", "2.0");
			if(result = Nil) {
				node->Insert("result", JsonElement->New(JsonElement->JsonType->NULL));
			}
			else {
				node->Insert("result", result);
			};

			if(id > -1) {
				node->Insert("id", id);
			};

			WriteString(node->ToString());
		}

		# --- request ---
		method : WriteRpcRequest(kind : String, result : JsonElement) ~ Nil {
			node := JsonElement->New(JsonElement->JsonType->OBJECT);

			node->Insert("jsonrpc", "2.0");
			node->Insert("method", kind);

			if(result = Nil) {
				node->Insert("params", JsonElement->New(JsonElement->JsonType->NULL));
			}
			else {
				node->Insert("params", result);
			};

			WriteString(node->ToString());
		}
	}

	#~
	Workspaace
	~#
	class : private : LspWorkspace {
		@name : String;
		@root : String;
		@local_path : String;
		@file_strs : String[];		
		@libs_str : String;
		
		@documents : Map<String, Document>;

		New() {
			@documents := Map->New()<String, Document>;
		}

		method : public : SetRoot(name : String, root : String) ~ Nil {
			@name := name;
			@root := root;
		}

		method : public : SetFilesLibs(local_path : String, file_strs : String[], libs_str : String) ~ Nil {
			@local_path := local_path;
			@file_strs := file_strs;
			@libs_str := libs_str;
		}

		# TODO
		method : public : GetTexts() ~ String[] {
			file_count := @file_strs->Size();

			if(file_count > 0) {
				texts := String->New[file_count];

				each(i : @file_strs) {
					file_str := String->New(@local_path);
					file_str += '/';
					file_str += @file_strs[i];

					text := System.IO.File.FileReader->ReadFile(file_str);

#					if(@is_debug) {
						"\n\n Tyring: {$file_str} \n\n"->PrintLine();
						"\n\n ### {$text} ### \n\n"->PrintLine();
#					};

					texts[i] := text;				
				};

				return texts;
			};

			return Nil;
		}

		method : public : GetDocument(uri : String) ~ Document {
			return @documents->Find(uri);
		}

		method : public : AddDocument(uri : String, document : Document) ~ Nil {
			@documents->Insert(uri, document);
		}

		method : public : RemoveDocument(uri : String, document : Document) ~ Nil {
			if(@documents->Remove(uri)) {
				document->Release();
			};
		}
	}

	#~
	In-memory text document
	~#
	class : private : Document {
		@workspace : LspWorkspace;
		@uri : String;
		@text : String;
		@alt_text : String;
		@is_debug : Bool;

		New(uri : String, text : String, workspace : LspWorkspace, is_debug : Bool) {
			@workspace := workspace;
			@uri := uri;
			@text := GetText(text);
			@is_debug := is_debug;
		}

		# TODO
		method : GatherBuffers() ~ String[] {
			return Nil;
		}
		
		method : public : GetDiagnosis() ~ JsonElement {
			proxy := AstProxy->New(@workspace, false, @is_debug);
			leaving {
				proxy->Release();
			};

			proxy->Build();
			return proxy->GetDiagnosis(@uri, "gen_collect.obl");
		}

		method : public : GetSymbols() ~ JsonElement {
			proxy := AstProxy->New(@workspace, false, @is_debug);
			leaving {
				proxy->Release();
			};

			if(<>proxy->Build()) {
				return Nil;
			};

			is_parsed := BoolHolder->New();
			result := proxy->GetSymbols(is_parsed, "gen_collect.obl");

			if(is_parsed->Get()) {
				@alt_text := @text->Clone()->As(String);
			};

			return result;
		}

		method : public : FindReferences(line_pos : Int, char_pos : Int) ~ JsonElement {
			proxy := AstProxy->New(@workspace, false, @is_debug);
			leaving {
				proxy->Release();
			};

			return proxy->FindReferences(line_pos, char_pos, @uri, "gen_collect.obl");
		}

		method : public : FindDeclaration(line_pos : Int, char_pos : Int) ~ JsonElement {
			proxy := AstProxy->New(@workspace, false, @is_debug);
			leaving {
				proxy->Release();
			};

			return proxy->FindDeclaration(line_pos, char_pos, @uri, "gen_collect.obl");
		}

		method : public : Completion(line_pos : Int, char_pos : Int) ~ JsonElement {
			proxy := AstProxy->New(@workspace, true, @is_debug);
			leaving {
				proxy->Release();
			};
			
			index := GetDocumentIndex(line_pos, char_pos);
			if(index > -1) {
				var_str := ""; mthd_str := "";
				if(GetVariableMethodStrings(index - 1, var_str, mthd_str, false)) {
					return proxy->Completion(line_pos, char_pos, var_str, mthd_str, @uri, "gen_collect.obl");
				};
			};

			return Nil;
		}


		method : public : SignatureHelp(line_pos : Int, char_pos : Int) ~ JsonElement {
			proxy := AstProxy->New(@workspace, true, @is_debug);
			leaving {
				proxy->Release();
			};

			index := GetDocumentIndex(line_pos, char_pos);
			if(index > -1) {
				var_str := "";	mthd_str := "";
				if(GetVariableMethodStrings(index, var_str, mthd_str)) {
					return proxy->SignatureHelp(line_pos, char_pos, var_str, mthd_str, @uri, "gen_collect.obl");
				};
			};
			
			return Nil;
		}

		method : public : GetVariableMethodStrings(index : Int, var_str : String, mthd_str : String, offset : Bool := true) ~ Bool {
			start_pos := index;
			while(@text->Get(index)->IsChar() | @text->Get(index) = '_'  | @text->Get(index) = '@' | @text->Get(index) = '(' | @text->Get(index) = ')') {
				index -= 1;
			};
			end_pos := index;

			length := start_pos - end_pos;
			if(offset) {
				length -= 2;
			};

			if(length > 0) {
				mthd_str += @text->SubString(end_pos + 1, length);
			};

			while(@text->Get(index) = ' ' | 
					(@text->Get(index) = 'n' & @text->Get(index - 1) = '\\') |
					(@text->Get(index) = 'r' & @text->Get(index - 1) = '\\') |
					(@text->Get(index) = 't' & @text->Get(index - 1) = '\\')) {
				index -= 1;
				if(@text->Get(index - 1) = '\\') {
					index -= 1;
				};
			};

			has_var := @text->Get(index) = '>' & @text->Get(index - 1) = '-';
			if(has_var) {
				index -= 2;
				start_pos := index;
				while(@text->Get(index)->IsChar() | @text->Get(index) = '_' |  @text->Get(index) = '@') {
					index -= 1;
				};
				end_pos := index;

				length := start_pos - end_pos;
				if(length > 0) {
					var_str += @text->SubString(end_pos + 1, length);
				};
			};

			if(@is_debug) {
				"\n\n--- var_str='{$var_str}', mthd_str='{$mthd_str}' ---\n\n"->PrintLine();
			};

			return <>(var_str->IsEmpty() & mthd_str->IsEmpty());
		}

		method : public : Update(range_length : Int, text : String, start_line : Int, start_char: Int, end_line : Int, end_char : Int) ~ Nil {
			start := GetDocumentIndex(start_line, start_char);
			if(@is_debug) {
				"### Update: start={$start}, range_length={$range_length}, text='{$text}'; start_line={$start_line}, start_char={$start_char}; end_line={$end_line}, end_char={$end_char} ###"->PrintLine();
			};
			
			if(start > -1) {
				if(range_length > 0) {
					@text->Delete(start, range_length);
				};
				@text->Insert(start, GetText(text));
			};
		}

		method : public : Save() ~ Nil {
			if(@is_debug) {
				"NotifyDidSave"->PrintLine();
			};
		}

		method : public : Release() ~ Nil {
			@text := Nil;
		}

		method : GetLineCharacterIndex(index : Int, line_pos : IntHolder, char_pos : IntHolder) ~ Nil {
			cur_line := 0;
			cur_char := 0;

			for(pos := 0; pos < index; pos += 1;) {
				char := @text->Get(pos);
				next_char := '\0';				
				if(pos + 1 < @text->Size()) {
					next_char := @text->Get(pos + 1);
				};

				if(char = '\\') {
					if(next_char = 'n') {
							cur_line += 1;
							cur_char := 0;
					};
					
					pos += 1;
					cur_char += 1;
				}
				else {
					cur_char += 1;
				};
			};

			line_pos->Set(cur_line);
			char_pos->Set(cur_char - 1);
		}

		method : GetDocumentIndex(line_pos : Int, char_pos : Int) ~ Int {
			if(line_pos = 0 & char_pos = 0) {
				return 0;
			};

			cur_line := 0; cur_char := 0;
			each(pos : @text) {
				char := @text->Get(pos);
				next_char := '\0';				
				if(pos + 1 < @text->Size()) {
					next_char := @text->Get(pos + 1);
				};

				if(char = '\n') {
					cur_line += 1;					
				};
				cur_char += 1;

				if(cur_line = line_pos) {
					index := cur_char + char_pos;
					if(cur_line = 0) {
						index -= 1;
					};
					
					return index;
				};
			};

			return -1;
		}

		method : GetText(text : String) ~ String {
			out := "";

			each(i : text) {
				cur_char := text->Get(i);
				next_char := '\0';
				if(i + 1 < text->Size()) {
					next_char := text->Get(i + 1);
				};

				if(cur_char = '\\') {
					select(next_char) {
						label 't' {
							out += '\t';
							i += 1;
						}

						label 'r' {
							out += '\r';
							i += 1;
						}

						label 'n' {
							out += '\n';
							i += 1;
						}

						label '\\' {
							out += '\\';
							i += 1;
						}

						label '"' {
							out += '"';
							i += 1;
						}

						other {
							out += cur_char;
						}
					};
				}
				else {
					out += cur_char;
				};
			};

			return out;
		}

		method : DumpDocument() ~ Nil {
			if(@is_debug) {
				"TEXT='{$@text}'"->PrintLine();
			};
		}
	}

	#~
	Handles JSON-RPC requests
	~#
	class : private : RpcRequest {
		@id : Int;
		@mthd : String;
		@params : JsonElement;

		New(id : Int, mthd : String, params : JsonElement) {
			@id := id;
			@mthd := mthd;
			@params := params;
			@is_debug := true;
		}

		method : public : GetId() ~ Int {
			return @id;
		}

		method : public : GetMethod() ~ String {
			return @mthd;
		}

		method : public : GetParams() ~ JsonElement {
			return @params;
		}
	}
}