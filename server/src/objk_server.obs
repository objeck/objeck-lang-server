use Collection.Generic;
use System.IO.Net;
use Data.JSON;
use Query.RegEx;

bundle LSP {
	class LintServer {
		@port : Int;
		@is_debug : Bool;

		function : Main(args : String[]) ~ Nil {
			if(args->Size() = 1) {
				LintService->Init();
				LintServer->New(args[0]->ToInt())->Listen();
			}
			else {
				"--- TODO: read config file ---"->ErrorLine();
			};
		}

		New(port : Int) {
			@port := port;
			@is_debug := true;
		}

		method : public : Listen() ~ Nil {
			server := TCPSocketServer->New(6013);
			leaving {
				server->Close();
			};

			if(server->Listen(5)) {
				while(true) {
					LintService->New(server->Accept(), @is_debug)->Execute(Nil);
				};
			};
		}
	}

	#~
	Handles requests
	~#
	class LintService from LspWorker {
		New(socket : TCPSocket, is_debug : Bool) {
			Parent(socket, is_debug);
		}

		#
		# Callbacks
		#
		method : CallbackReferences(id : Int, params : JsonElement) ~ Nil {
			uri := params->FindElements("textDocument/uri")->GetValue();
			line_pos := params->FindElements("position/line")->GetValue();
			char_pos:= params->FindElements("position/character")->GetValue();

			response := GetDocument(uri)->GetReferences(line_pos->ToInt(), char_pos->ToInt());
			WriteRpcResponse(id, response);
		}


		method : CallbackInitialize(id : Int, params : JsonElement) ~ Nil {
			workspace := params->FindElements("capabilities/workspace");
			configuration := params->FindElements("capabilities/workspace/configuration");

			# build response
			response := JsonElement->New(JsonElement->JsonType->OBJECT);
			capabilities := response->AddChild("capabilities");
	        completionProvider := capabilities->AddChild("completionProvider");
	        completionProvider->Insert("resolveProvider", true);


	        workspace := capabilities->AddChild("workspace");
	        workspaceFolders := workspace->AddChild("workspaceFolders");
	        workspaceFolders->Insert("supported", true);

	        capabilities->Insert("textDocumentSync", 2);
	        capabilities->Insert("documentSymbolProvider", true);
	        capabilities->Insert("referencesProvider", true);
	        
	        # write response
	        WriteRpcResponse(id, response);
		}

		method : CallbackShutdown(id : Int, params : JsonElement) ~ Nil {
			if(IsDebug()) {
				"=> CallbackShutdown"->PrintLine();
			};

			@quit := true;
		}	

		method : CallbackCompletion(id : Int, params : JsonElement) ~ Nil {
			uri := params->FindElements("textDocument/uri");
			line_pos := params->FindElements("position/line");
			char_id := params->FindElements("position/character");
			kind := params->FindElements("context/triggerKind");

			if(IsDebug()) {
				"CallbackCompletion => file='{$uri}', line={$line_pos}, char={$char_id}, kind={$kind}"->PrintLine();
			};

			keywords := GetKeywords();
			response := JsonElement->New(JsonElement->JsonType->ARRAY);		
			each(i : keywords) {
				item := JsonElement->New(JsonElement->JsonType->OBJECT);
				keyword := keywords->Get(i);
				item->Insert("label", keyword);
				item->Insert("kind", 7);
				item->Insert("data", i);
				item->Insert("detail", "{$keyword} details");
	      		item->Insert("documentation", "{$keyword} documentation");      		
				response->Add(item);
			};

	        # write response
	        WriteRpcResponse(id, response);
		}

		method : CallbackDocumentSymbol(id : Int, params : JsonElement) ~ Nil {
			uri := params->FindElements("textDocument/uri")->GetValue();
			response := GetDocument(uri)->GetSymbols();
			WriteRpcResponse(id, response);
#~
			response := JsonElement->New(JsonElement->JsonType->ARRAY);
			each(i : 2) {
				symbol := symbols->Get(i);

				item := JsonElement->New(JsonElement->JsonType->OBJECT);

				name := "Foo_";
				name += i;

				item->Insert("name", name);
				item->Insert("kind", 12);
				
				range := JsonElement->New(JsonElement->JsonType->OBJECT);
				
				start := JsonElement->New(JsonElement->JsonType->OBJECT);
				start->Insert("line", 5);
				start->Insert("character", 1);
				range->Insert("start", start);

				end := JsonElement->New(JsonElement->JsonType->OBJECT);
				end->Insert("line", 5);
				end->Insert("character", 6);
				range->Insert("end", end);

				location := JsonElement->New(JsonElement->JsonType->OBJECT);
				location->Insert("range", range);				
				location->Insert("selectionRange", range);

				item->Insert("location", location);
				response->Add(item);
			};
~#			
		}

		method : CallbackResolve(id : Int, params : JsonElement) ~ Nil {
			# write response
	        WriteRpcResponse(id, params);
		}

		#
		# Notifications ---
		#
		method : NotifyInitialized(params : JsonElement) ~ Nil {
			@is_initialized := true;
		}

		method : NotifyCancel(params : JsonElement) ~ Nil {
				
		}

		method : NotifyDidChangeWatchedFiles(params : JsonElement) ~ Nil {

		}

		method : NotifyDidOpen(params : JsonElement) ~ Nil {
			uri_elem := params->FindElements("textDocument/uri");
			text_elem := params->FindElements("textDocument/text");

			if(uri_elem <> Nil & text_elem <> Nil) {
				uri := uri_elem->GetValue();
				text := text_elem->GetValue();

				if(IsDebug()) {
					"NotifyDidOpen => text='{$text}"->PrintLine();
				};

				document := Document->New(uri, text);
				AddDocument(uri, document);
			}
			else {
				"*** Failed: NotifyDidOpen ***"->ErrorLine();				
			};
		}

		method : NotifyDidSave(params : JsonElement) ~ Nil {
			document := GetDocument(params->FindElements("textDocument/uri")->GetValue());
			if(document <> Nil) {
				document->Save();
			};
		}

		method : NotifyDidChange(params : JsonElement) ~ Nil {
			if(IsDebug()) {
				"NotifyDidChange"->PrintLine();
			};

			uri := params->FindElements("textDocument/uri")->GetValue();
			document := GetDocument(uri);
			changes := params->FindElements("contentChanges");
			if(document <> Nil & changes <> Nil) {
				each(i : changes) {
					change := changes->Get(i);

					range_length := change->Get("rangeLength")->GetValue();
					text := change->Get("text")->GetValue();

					start_line := change->FindElements("range/start/line")->GetValue();
					start_char := change->FindElements("range/start/character")->GetValue();

					end_line := change->FindElements("range/end/line")->GetValue();
					end_char := change->FindElements("range/end/character")->GetValue();
					
					if(range_length <> Nil & text <> Nil & start_line <> Nil & start_char <> Nil  & end_line <> Nil & end_char <> Nil) {
						document->Update(range_length->ToInt(), text, start_line->ToInt(), start_char->ToInt(),	end_line->ToInt(), end_char->ToInt());
					};
				};

				WriteRpcRequest("textDocument/publishDiagnostics", document->GetDiagnoses());
			};
		}
	}

	#~
	In-memory document
	~#
	class : private : Document {
		@uri : String;
		@text : String;
		@is_debug : Bool;

		New(uri : String, text : String) {
			@uri := uri;
			@text := text;
		}

		# TODO: hacked...
		method : public : GetReferences(line_pos : Int, char_pos : Int) ~ JsonElement {
out := Web.HTTP.Url->Decode(@uri);
			out->Delete(0, "file:///"->Size());
			out->PrintLine();
"############# {$out} #############"->PrintLine();

			index := GetDocumentIndex(line_pos, char_pos);

			string := @text;
			substring : String;
			if(index > -1 & index < string->Size()) {
				start := index;
				while(start > -1 & (string->Get(start)->IsChar() | string->Get(start) = '@' | string->Get(start) = '_')) {
					start -= 1;
				};

				end := index;
				while(end < string->Size() & (string->Get(end)->IsChar() | string->Get(end) = '@' | string->Get(end) = '_')) {
					end += 1;
				};

				if(start <> end) {
					substring := string->SubString(start + 1, end - start - 1);
					"pos: {$start},{$end} '{$substring}'"->PrintLine();
				};
			};

			parser := Parser->New(out);
			if(parser->Parse()) {
				obj := parser->GetReferences(substring, @uri);
"############# {$obj} #############"->PrintLine();			
				return obj;
			};

			return Nil;
		}

		method : public : GetSymbols() ~ JsonElement {
			out := Web.HTTP.Url->Decode(@uri);
			out->Delete(0, "file:///"->Size());
			out->PrintLine();
"############# {$out} #############"->PrintLine();	

			parser := Parser->New(out);
			if(parser->Parse()) {
				obj := parser->GetSymbols();
"############# {$obj} #############"->PrintLine();			
				return obj;
			};

			return Nil;
		}

		method : public : Update(range_length : Int, text : String, start_line : Int, start_char: Int, end_line : Int, end_char : Int) ~ Nil {
			if(@is_debug) {
				"### Update: range_length={$range_length}, text='{$text}'; start_line={$start_line}, start_char={$start_char}; end_line={$end_line}, end_char={$end_char} ###"->PrintLine();
			};

			start := GetDocumentIndex(start_line, start_char);
			if(start > -1) {
				if(range_length > 0) {
					@text->Delete(start, range_length);
				};
				@text->Insert(start, text);
			};
		}

		method : public : Save() ~ Nil {
			if(@is_debug) {
				"NotifyDidSave"->PrintLine();
			};
		}

		method : public : GetDiagnoses() ~ JsonElement {
			results := RegEx->New("function(\\s*):(\\s*)(\\w+)")->Find(@text)<Result>;
			diagnostics := JsonElement->New(JsonElement->JsonType->ARRAY);
			each(i : results) {
				result := results->Get(i);

				line_pos := IntHolder->New(); 
				char_pos := IntHolder->New();
				GetLineCharacterIndex(result->GetStart(), line_pos, char_pos);

				diagnostic := JsonElement->New(JsonElement->JsonType->OBJECT);

				diagnostic->Insert("severity", 1);
				diagnostic->Insert("message", "Is a function");
				diagnostic->Insert("source", "objk-lint");

				range := JsonElement->New(JsonElement->JsonType->OBJECT);
				# start
				start := JsonElement->New(JsonElement->JsonType->OBJECT);
				start->Insert("line", line_pos->Get());
				start->Insert("character", char_pos->Get());
				range->Insert("start", start);
				# end
				end := JsonElement->New(JsonElement->JsonType->OBJECT);
				end->Insert("line", line_pos->Get());
				end->Insert("character", char_pos->Get() + result->ToString()->Size());
				range->Insert("end", end);
				diagnostic->Insert("range", range);
				
				diagnostics->Add(diagnostic);
			};
			
			request := JsonElement->New(JsonElement->JsonType->OBJECT);
			request->Insert("uri", @uri);
			request->Insert("diagnostics", diagnostics);
						
			return request;
		}

		method : GetLineCharacterIndex(index : Int, line_pos : IntHolder, char_pos : IntHolder) ~ Nil {
			cur_line := 0;
			cur_char := 0;

			for(pos := 0; pos < index; pos += 1;) {
				char := @text->Get(pos);
				next_char := '\0';				
				if(pos + 1 < @text->Size()) {
					next_char := @text->Get(pos + 1);
				};

				if(char = '\\') {
					if(next_char = 'n') {
							cur_line += 1;
							cur_char := 0;
					};
					
					pos += 1;
					cur_char += 1;
				}
				else {
					cur_char += 1;
				};
			};

			line_pos->Set(cur_line);
			char_pos->Set(cur_char - 1);
		}

		method : GetDocumentIndex(line_pos : Int, char_pos : Int) ~ Int {
			cur_line := 0;
			cur_char := 0;

			each(pos : @text) {
				char := @text->Get(pos);
				next_char := '\0';				
				if(pos + 1 < @text->Size()) {
					next_char := @text->Get(pos + 1);
				};

				if(char = '\\') {
					if(next_char = 'n') {
							cur_line += 1;
					};
					
					pos += 1;
					cur_char += 2;
				}
				else {
					cur_char += 1;
				};

				if(cur_line = line_pos) {
					index := cur_char + char_pos;
					if(cur_line = 0) {
						index -= 1;
					};
					
					return index;
				};
			};

			return -1;
		}

		method : DumpDocument() ~ Nil {
			if(@is_debug) {
				"TEXT='{$@text}'"->PrintLine();
			};
		}
	}
}